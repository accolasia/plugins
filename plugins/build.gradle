apply plugin: 'groovy'
apply plugin: 'maven-publish'


File uploadDir = file('build/upload')

repositories {
	mavenCentral()
}

configurations {
    ftpAntTask
}

sourceSets {
	main {
		groovy {
			srcDir 'common/src'
		}
		resources {
			srcDir 'common/resources'
		}
	}

	test {
		groovy {
			srcDir 'common/test'
		}
	}
}

dependencies {
	ftpAntTask("org.apache.ant:ant-commons-net:1.9.3") {
		module("commons-net:commons-net:1.4.1") {
			dependencies "oro:oro:2.0.8:jar"
		}
	}

	compile 'org.codehaus.groovy:groovy-all:1.8.6'
	compile 'joda-time:joda-time:2.3'

	testCompile 'junit:junit:4.8.1'
	testCompile 'org.reflections:reflections:0.9.9-RC1'
	testCompile 'org.slf4j:slf4j-api:1.7.5'
}

jar.baseName = 'ideaflow-common'


task prepareRuntimeLibs(type: Copy) {
	into "common/libs/runtime"
	from configurations.runtime
}

task prepareTestLibs(type: Copy) {
	into "common/libs/test"
	from configurations.testRuntime
}

task prepareLibs {
     dependsOn { [prepareRuntimeLibs, prepareTestLibs] }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId project.ext.artifactId
            from components.java
        }
    }
}

task publishLocal(dependsOn: publishToMavenLocal)


String ftpPassword = null
task upload(dependsOn: ['createUpdatePluginXmlFile', 'copyAndRenameIntellijPlugin']) {
	doFirst {
		def console = System.console()
		if (console != null) {
			ftpPassword = console.readPassword('> Please enter ideaflow.org FTP password: ') as String
		} else {
			throw new GradleException("System.console is null, make sure you're not running gradle in daemon mode")
		}
	}
	doLast {
		ant {
			taskdef(name: 'ftp',
					classname: 'org.apache.tools.ant.taskdefs.optional.net.FTP',
					classpath: configurations.ftpAntTask.asPath)
			ftp(server: 'ideaflow.org', userid: 'ideaflow', password: ftpPassword, passive: 'yes') {
				fileset(dir: 'build/upload')
			}
		}
	}
}

task copyAndRenameIntellijPlugin(type: Copy) {
	from 'intellij/ideaflow-plugin.zip'
	into uploadDir
	rename ('ideaflow-plugin.zip', 'ideaflow.zip')
}

task createUpdatePluginXmlFile << {

	String pluginVersion = getPluginVersion()

	uploadDir.mkdirs()
	new File(uploadDir, 'updatePlugins.xml').write """<plugins>
    <plugin id="org.ideaflow" url="http://ideaflow.org/ideaflow.zip" version="${pluginVersion}"/>
</plugins>
"""

}

private String getPluginVersion() {
	String pluginXmlContent = file('intellij/META-INF/plugin.xml').text
	def matcher = pluginXmlContent =~ /(?m).version.(\d+\.\d+)..version./
	if (!matcher) {
		throw new GradleException("Unable to determine plugin version")
	}
	matcher.group(1)
}